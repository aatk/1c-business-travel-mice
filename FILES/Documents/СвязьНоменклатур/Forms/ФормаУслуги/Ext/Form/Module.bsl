

&НаСервере
Процедура ЗаполнитьСкрытыеСтрокиОписанияУслуг()
	
	ОтборОписания = Новый Структура("НеОтображать", Истина);		
	СтрокиХарактеристики = Объект.НоменклатураБТ.Характеристики.НайтиСтроки(ОтборОписания);		
		
	Для Каждого СтрокаНастроек ИЗ СтрокиХарактеристики Цикл		
		ОтборОписания = Новый Структура("Характеристика", СтрокаНастроек.Характеристика);		
		Строки = Объект.ОписаниеУслуги.НайтиСтроки(ОтборОписания);		
		Если Строки.Количество() > 0 Тогда
			Для Каждого Строка ИЗ Строки Цикл 
				Строка.СкрытаяСтрока = Истина;
			КонецЦикла;	
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	
	
&НаСервере
Процедура ЗаполнитьХарактеристикиНаСервере(НоменклатураБТ)
	
	СписокХарактеристик = Новый Массив;
	
	СтарыеХарактеристики = Объект.ОписаниеУслуги.Выгрузить();
	Объект.ОписаниеУслуги.Очистить();
	
	Для Каждого СтрокаНастроек ИЗ НоменклатураБТ.Характеристики Цикл		
		
		Если СтрокаНастроек.Характеристика <> ПланыВидовХарактеристик.НаборХарактеристикДляНоменклатуры.IDСинхронизации Тогда 
			НоваяСтрока = Объект.ОписаниеУслуги.Добавить();		
			НоваяСтрока.Характеристика = СтрокаНастроек.Характеристика;
			
			ОтборОписания = Новый Структура("Характеристика", СтрокаНастроек.Характеристика);		
			Строки = СтарыеХарактеристики.НайтиСтроки(ОтборОписания);		
			Если Строки.Количество() > 0 Тогда
				Для Каждого Строка ИЗ Строки Цикл 
					НоваяСтрока.Значение = Строка.Значение;
				КонецЦикла;	
			КонецЕсли;
			
			Если СтрокаНастроек.ХранитьИсторию Тогда
				СписокХарактеристик.Добавить(СтрокаНастроек.Характеристика);
			КонецЕсли;
			
			Если СтрокаНастроек.НеОтображать Тогда
				СписокСкрытых.Добавить(СтрокаНастроек.Характеристика);
			КонецЕсли;
			
			Если СтрокаНастроек.ОбязательноеЗаполнение Тогда
				СписокОбязательных.Добавить(СтрокаНастроек.Характеристика);
			КонецЕсли;
			
			НоваяСтрока.СкрытаяСтрока = СтрокаНастроек.НеОтображать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураБТПриИзменении(Элемент)
	ЗаполнитьХарактеристикиНаСервере(Объект.НоменклатураБТ);
КонецПроцедуры







&НаСервере
Процедура ЗаполнитьЭкстраПоляНаСервере()
	
	ТаблицаПолей = УправлениеБТСервер.ПолучитьТаблицуЭкстраПолей(
		Объект.Дата,
		Объект.Партнер,
		Объект.Контрагент,
		Объект.Договор);
		
	Объект.ЭкстраПоля.Загрузить(ТаблицаПолей);
	
	УслугаОбъект = РеквизитФормыВЗначение("Объект");
	УправлениеБТСервер.ЗаполнитьЭкстраПоляАвтоматически(УслугаОбъект, УслугаОбъект.ЭкстраПоля);
	ЗначениеВРеквизитФормы(УслугаОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЭкстраПоля(Команда)
	ЗаполнитьЭкстраПоляНаСервере();
	ЭтаФорма.Модифицированность = Истина;
	Оповестить("ИзменениеТабличнойЧастиУслуги", Объект);
КонецПроцедуры




&НаСервере
Процедура ПоказатьСкрытыеНаСервере()	
	ЭтаФорма.Элементы.ОписаниеУслуги.ОтборСтрок = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСкрытые(Команда)
	ЭтаФорма.Элементы.ОписаниеУслугиПоказатьСкрытые.Видимость = Ложь;
	ЭтаФорма.Элементы.ОписаниеУслугиСкрытьНеобязательные.Видимость = Истина;
	ПоказатьСкрытыеНаСервере();
КонецПроцедуры

&НаСервере
Процедура СкрытьНеобязательныеНаСервере()
	ОтборСтрок = Новый ФиксированнаяСтруктура("СкрытаяСтрока", Ложь);
	ЭтаФорма.Элементы.ОписаниеУслуги.ОтборСтрок = ОтборСтрок;
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНеобязательные(Команда)
	ЭтаФорма.Элементы.ОписаниеУслугиСкрытьНеобязательные.Видимость = Ложь;
	ЭтаФорма.Элементы.ОписаниеУслугиПоказатьСкрытые.Видимость = Истина;
	СкрытьНеобязательныеНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ПоставщикНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФормаКонтрагентов = ПолучитьФорму("Справочник.Контрагенты.ФормаВыбора");
	ЭлементОтбора = ФормаКонтрагентов.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Партнер.Поставщик");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементОтбора.ПравоеЗначение = Истина;
	ФормаКонтрагентов.Параметры.РежимВыбора = Истина;
	ФормаКонтрагентов.ВладелецФормы = ЭтаФорма.Элементы.Поставщик;
	ФормаКонтрагентов.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФормаКонтрагентов = ПолучитьФорму("Справочник.Контрагенты.ФормаВыбора");
    ЭлементОтбора = ФормаКонтрагентов.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Партнер.Клиент");
    ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
    ЭлементОтбора.Использование = Истина;
    ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
    ЭлементОтбора.ПравоеЗначение = Истина;	
	ФормаКонтрагентов.Параметры.РежимВыбора = Истина;
	ФормаКонтрагентов.ВладелецФормы = ЭтаФорма.Элементы.Контрагент;
	ФормаКонтрагентов.Открыть();
	
КонецПроцедуры





#Область ИзменениеЧисловыхДанныхУслуги


// ПОКАЗАТЕЛИ СТОИМОСТИ ПОСТАВЩИКА 

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	
	СуммаПоставщика = ЦенаПоставщика;
	
	СтавкаНДСПриИзмененииНаСервере();
	
	ЦенаКлиента = ЦенаПоставщика;
	СуммаКлиента = СуммаПоставщика;
	СтавкаНДСКлиентаПриИзмененииНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	СтавкаНДСПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СтавкаНДСПриИзмененииНаСервере()
	
	Если ТипЗнч(ЦенаПоставщика) = Тип("Число") И ТипЗнч(СуммаПоставщика) = Тип("Число") Тогда 
		НоваяСуммаНДС = УправлениеБТВызовСервера.ПолучитьСуммуНДС(СуммаПоставщика, Объект.СтавкаНДС, Истина); 
		Объект.СуммаНДС = НоваяСуммаНДС;
	
		Объект.СтавкаНДСКлиента = Объект.СтавкаНДС;
		СтавкаНДСКлиентаПриИзмененииНаСервере(); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	СуммаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СуммаПриИзмененииНаСервере()
	ЦенаПоставщика = СуммаПоставщика;
	СтавкаНДСПриИзмененииНаСервере();	
КонецПроцедуры


// КЛИЕНТСКИЕ ПОКАЗАТЕЛИ СТОИМОСТИ
&НаСервере
Процедура СтавкаНДСКлиентаПриИзмененииНаСервере()
	Если ТипЗнч(ЦенаКлиента) = Тип("Число") Тогда 
		СуммаНДСКлиента = УправлениеБТСервер.ВычислитьСуммуНДС(ЦенаКлиента, Объект.СтавкаНДСКлиента);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСКлиентаПриИзменении(Элемент)
	СтавкаНДСКлиентаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЦенаКлиентаПриИзменении(Элемент)
	СуммаКлиента = ЦенаКлиента;
	СтавкаНДСКлиентаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СуммаКлиентаПриИзменении(Элемент)
	Если ТипЗнч(ЦенаКлиента) = Тип("Число") И ТипЗнч(СуммаКлиента) = Тип("Число") И ТипЗнч(КоличествоКлиента) = Тип("Число") Тогда 
		ЦенаКлиента = СуммаКлиента / ?(ЗначениеЗаполнено(КоличествоКлиента), КоличествоКлиента, 1);
		СтавкаНДСКлиентаПриИзмененииНаСервере();
	КонецЕсли;		
КонецПроцедуры


&НаСервере
Процедура КоличествоКлиентаПриИзмененииНаСервере()
	Если ТипЗнч(ЦенаКлиента) = Тип("Число") И ТипЗнч(СуммаКлиента) = Тип("Число") Тогда 
		ЦенаКлиента = СуммаКлиента / КоличествоКлиента;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КоличествоКлиентаПриИзменении(Элемент)
	КоличествоКлиентаПриИзмененииНаСервере();
КонецПроцедуры


#КонецОбласти





&НаСервере
Функция ЗаписатьНастройкиНаСервере()
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Настройки = Новый Структура("ВерхнийУровень, Глубина", Истина, 0); //("ВерхнийУровень", Истина);
	НовыйОбъект = ИИСервер.ВернутьОписаниеСсылки(ОбъектСервер, Неопределено, Неопределено, Настройки);
	
	Если ТипЗнч(ДатаДокумента) <> Тип("Дата") И ДатаДокумента <> Неопределено  Тогда
		Настройки = Новый Структура("ВерхнийУровень, Глубина", Истина, 0);
		ДатаДокументаСтруктура = ИИСервер.ВернутьОписаниеСсылки(ДатаДокумента, Неопределено, Неопределено, Настройки);		
		НовыйОбъект.Дата = ДатаДокументаСтруктура;
	Иначе
		НовыйОбъект.Дата = ДатаДокумента;		
	КонецЕсли;
	
	//Поставщик
	Если ТипЗнч(ЦенаПоставщика) <> Тип("Число") И ЦенаПоставщика <> Неопределено  Тогда
		Настройки = Новый Структура("ВерхнийУровень, Глубина", Истина, 0);
		ЦенаПоставщикаСтруктура = ИИСервер.ВернутьОписаниеСсылки(ЦенаПоставщика, Неопределено, Неопределено, Настройки);		
		НовыйОбъект.Цена = ЦенаПоставщикаСтруктура;
	Иначе
		НовыйОбъект.Цена = ЦенаПоставщика;		
	КонецЕсли;
	Если ТипЗнч(СуммаНДСПоставщика) <> Тип("Число") И СуммаНДСПоставщика <> Неопределено Тогда
		Настройки = Новый Структура("ВерхнийУровень, Глубина", Истина, 0);
		СуммаНДСПоставщикаСтруктура = ИИСервер.ВернутьОписаниеСсылки(СуммаНДСПоставщика, Неопределено, Неопределено, Настройки);		
		НовыйОбъект.СуммаНДС = СуммаНДСПоставщикаСтруктура;
	Иначе
		НовыйОбъект.СуммаНДС = СуммаНДСПоставщика;
	КонецЕсли;
	Если ТипЗнч(СуммаПоставщика) <> Тип("Число") И СуммаПоставщика <> Неопределено  Тогда
		Настройки = Новый Структура("ВерхнийУровень, Глубина", Истина, 0);
		СуммаПоставщикаСтруктура = ИИСервер.ВернутьОписаниеСсылки(СуммаПоставщика, Неопределено, Неопределено, Настройки);		
		НовыйОбъект.Сумма = СуммаПоставщикаСтруктура;
	Иначе
		НовыйОбъект.Сумма = СуммаПоставщика;
	КонецЕсли;
	
	//Клиент
	Если ТипЗнч(ЦенаКлиента) <> Тип("Число") И ЦенаКлиента <> Неопределено Тогда
		Настройки = Новый Структура("ВерхнийУровень, Глубина", Истина, 0);
		ЦенаКлиентаСтруктура = ИИСервер.ВернутьОписаниеСсылки(ЦенаКлиента, Неопределено, Неопределено, Настройки);		
		НовыйОбъект.ЦенаКлиента = ЦенаКлиентаСтруктура;
	Иначе
		НовыйОбъект.ЦенаКлиента = ЦенаКлиента;		
	КонецЕсли;
	Если ТипЗнч(КоличествоКлиента) <> Тип("Число") И КоличествоКлиента <> Неопределено Тогда
		Настройки = Новый Структура("ВерхнийУровень, Глубина", Истина, 0);
		КоличествоКлиентаСтруктура = ИИСервер.ВернутьОписаниеСсылки(КоличествоКлиента, Неопределено, Неопределено, Настройки);		
		НовыйОбъект.КоличествоКлиента = КоличествоКлиентаСтруктура;
	Иначе
		НовыйОбъект.КоличествоКлиента = КоличествоКлиента;		
	КонецЕсли;
	Если ТипЗнч(СуммаНДСКлиента) <> Тип("Число") И СуммаНДСКлиента <> Неопределено Тогда
		Настройки = Новый Структура("ВерхнийУровень, Глубина", Истина, 0);
		СуммаНДСКлиентаСтруктура = ИИСервер.ВернутьОписаниеСсылки(СуммаНДСКлиента, Неопределено, Неопределено, Настройки);		
		НовыйОбъект.СуммаНДСКлиента = СуммаНДСКлиентаСтруктура;
	Иначе
		НовыйОбъект.СуммаНДСКлиента = СуммаНДСКлиента;		
	КонецЕсли;
	Если ТипЗнч(СуммаКлиента) <> Тип("Число") И СуммаКлиента <> Неопределено Тогда
		Настройки = Новый Структура("ВерхнийУровень, Глубина", Истина, 0);
		СуммаКлиентаСтруктура = ИИСервер.ВернутьОписаниеСсылки(СуммаКлиента, Неопределено, Неопределено, Настройки);		
		НовыйОбъект.СуммаКлиента = СуммаКлиентаСтруктура;
	Иначе
		НовыйОбъект.СуммаКлиента = СуммаКлиента;		
	КонецЕсли;
	
	Если Командируемые.Количество()>0 Тогда
		КомандируемыеСервер = РеквизитФормыВЗначение("Командируемые");
		Настройки = Новый Структура("ВерхнийУровень, Глубина", Истина, 0);
		КомандируемыеМассив = ИИСервер.ПолучитьЗначениеРеквизита(КомандируемыеСервер, Неопределено, Настройки);		
		НовыйОбъект.Командируемые = Новый Структура("СУсловиями", КомандируемыеМассив);
	Иначе
		//НовыйОбъект.СуммаКлиента = СуммаКлиента;		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтатусУслуги) Тогда
		Настройки = Новый Структура("ВерхнийУровень, Глубина", Истина, 0);
		СтатусУслугиСтруктура = ИИСервер.ВернутьОписаниеПеречисления(СтатусУслуги, Неопределено, Неопределено, Настройки);		
		НовыйОбъект.Вставить("СтатусУслуги", СтатусУслугиСтруктура);
	КонецЕсли;
	
	УслугаJSON = УправлениеБТВызовСервера.ВJSON(НовыйОбъект);
	
	Возврат УслугаJSON;
КонецФункции

&НаСервере
Функция СформироватьНастройкиДляСовместимостиНаСервере()
	
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	
	СтарыеПараметры = Новый Структура("СвязаннаяНоменклатура, НоменклатураПриемник, ПрименяетсяК, РеквизитУстановкиЦены, Цена, Сумма, СуммаНДС, СтавкаНДС");
	
	СтарыеПараметры.Вставить("СвязаннаяНоменклатура", УсловиеДляФормированияУслуги); //Не совсем обратная совместимость, но по другому никак	
	СтарыеПараметры.Вставить("НоменклатураПриемник", Объект.НоменклатураБТ); //Не совсем обратная совместимость, но по другому никак	
	
	Если ТипЗнч(ЦенаКлиента) = Тип("СправочникСсылка.ФункцииОписания") Тогда
		СтарыеПараметры.Вставить("ПрименяетсяК", ЦенаКлиента);
		СтарыеПараметры.Вставить("РеквизитУстановкиЦены", "Цена");
		СтарыеПараметры.Вставить("Цена", 0);
		СтарыеПараметры.Вставить("Сумма", 0);
		
	ИначеЕсли ТипЗнч(ЦенаКлиента) = Тип("СправочникСсылка.ЭлементОтбора") Тогда
		СтарыеПараметры.Вставить("ПрименяетсяК", ЦенаКлиента);
		СтарыеПараметры.Вставить("РеквизитУстановкиЦены", "Цена");
		СтарыеПараметры.Вставить("Цена", 0);
		СтарыеПараметры.Вставить("Сумма", 0);
	Иначе
		Если ТипЗнч(СуммаНДСКлиента) = Тип("СправочникСсылка.ЭлементОтбора") Тогда 
			СтарыеПараметры.Вставить("ПрименяетсяК", СуммаНДСКлиента);
			СтарыеПараметры.Вставить("РеквизитУстановкиЦены", "СуммаНДС18");
			
		Иначе	
			СтарыеПараметры.Вставить("Цена", СуммаКлиента);
			СтарыеПараметры.Вставить("СуммаНДС", СуммаНДСКлиента);
			СтарыеПараметры.Вставить("Сумма", СуммаКлиента);
		КонецЕсли;
	КонецЕсли;
	
	СтарыеПараметры.Вставить("СтавкаНДС", Объект.СтавкаНДС);
		
	Возврат СтарыеПараметры;
КонецФункции

&НаКлиенте
Процедура ЗаписатьНастройки(Команда)
	
	УслугаJSON = ЗаписатьНастройкиНаСервере();
	СтарыеПараметры = СформироватьНастройкиДляСовместимостиНаСервере();
	ПредставлениеШаблона = "Услуга (" + Строка(Объект.НоменклатураБТ) + ") Условие ("+ Строка(УсловиеДляФормированияУслуги) +") и Сумма клиенту (" +Строка(СуммаКлиента)+")"; 
	ЭтаФорма.Закрыть(Новый Структура("УслугаJSON, ПредставлениеШаблона, УсловиеДляФормированияУслуги, СтарыеПараметры", УслугаJSON, ПредставлениеШаблона, УсловиеДляФормированияУслуги, СтарыеПараметры));
	
КонецПроцедуры


&НаКлиенте
Процедура ЗакрытьБезЗаписи(Команда)
	ЭтаФорма.Закрыть();
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.Параметры.Свойство("УсловиеДляФормированияУслуги", УсловиеДляФормированияУслуги);
	
	УслугаJSON = Неопределено;	
	ЭтаФорма.Параметры.Свойство("ШаблонСвязаннойУслуги", УслугаJSON);
	Если ЗначениеЗаполнено(УслугаJSON) Тогда
		
		НовыйОбъект = УправлениеБТВызовСервера.ИзJSON(УслугаJSON);
		
		Если ЗначениеЗаполнено(НовыйОбъект.Дата) Тогда
			ДатаДокумента = ИИСервер.ПолучитьСсылкуНаОбъект(НовыйОбъект.Дата);
		Иначе
			ДатаДокумента = Дата("00010101");
		КонецЕсли;
		НовыйОбъект.Дата = Дата("00010101");			
		
		//Поставщик
		Если ТипЗнч(НовыйОбъект.Цена) = Тип("Структура") Тогда
			ЦенаПоставщика = ИИСервер.ПолучитьСсылку(НовыйОбъект.Цена);
			НовыйОбъект.Цена = 0;			
		Иначе
			ЦенаПоставщика = НовыйОбъект.Цена;			
		КонецЕсли;
		Если ТипЗнч(НовыйОбъект.СуммаНДС) = Тип("Структура") Тогда
			ЦенаПоставщика = ИИСервер.ПолучитьСсылку(НовыйОбъект.СуммаНДС);
			НовыйОбъект.СуммаНДС = 0;			
		Иначе
			СуммаНДСПоставщика = НовыйОбъект.СуммаНДС;			
		КонецЕсли;
		Если ТипЗнч(НовыйОбъект.Сумма) = Тип("Структура") Тогда
			СуммаПоставщика = ИИСервер.ПолучитьСсылку(НовыйОбъект.Сумма);
			НовыйОбъект.Сумма = 0;			
		Иначе
			СуммаПоставщика = НовыйОбъект.Сумма;			
		КонецЕсли;
		
		//Клиент
		Если ТипЗнч(НовыйОбъект.ЦенаКлиента) = Тип("Структура") Тогда
			ЦенаКлиента = ИИСервер.ПолучитьСсылку(НовыйОбъект.ЦенаКлиента);
			НовыйОбъект.ЦенаКлиента = 0;			
		Иначе
			ЦенаКлиента = НовыйОбъект.ЦенаКлиента;			
		КонецЕсли;
		Если ТипЗнч(НовыйОбъект.КоличествоКлиента) = Тип("Структура") Тогда
			КоличествоКлиента = ИИСервер.ПолучитьСсылку(НовыйОбъект.КоличествоКлиента);
			НовыйОбъект.КоличествоКлиента = 0;			
		Иначе
			КоличествоКлиента = НовыйОбъект.КоличествоКлиента;			
		КонецЕсли;
		Если ТипЗнч(НовыйОбъект.СуммаНДСКлиента) = Тип("Структура") Тогда
			СуммаНДСКлиента = ИИСервер.ПолучитьСсылку(НовыйОбъект.СуммаНДСКлиента);
			НовыйОбъект.СуммаНДСКлиента = 0;			
		Иначе
			СуммаНДСКлиента = НовыйОбъект.СуммаНДСКлиента;			
		КонецЕсли;
		Если ТипЗнч(НовыйОбъект.СуммаКлиента) = Тип("Структура") Тогда
			СуммаКлиента = ИИСервер.ПолучитьСсылку(НовыйОбъект.СуммаКлиента);
			НовыйОбъект.СуммаКлиента = 0;			
		Иначе
			СуммаКлиента = НовыйОбъект.СуммаКлиента;			
		КонецЕсли;
		
		Если ТипЗнч(НовыйОбъект.Командируемые) = Тип("Структура") Тогда
			КомандируемыеМассив = НовыйОбъект.Командируемые.СУсловиями;
			КомандируемыеТЗ = ИИСервер.ПолучитьСсылкуНаОбъект(КомандируемыеМассив);
			ЗначениеВРеквизитФормы(КомандируемыеТЗ, "Командируемые");
		Иначе
			//СуммаКлиента = НовыйОбъект.СуммаКлиента;			
		КонецЕсли;
		
		Если НовыйОбъект.Свойство("СтатусУслуги") И ТипЗнч(НовыйОбъект.СтатусУслуги) = Тип("Структура") Тогда
			СтатусУслуги = ИИСервер.ПолучитьСсылку(НовыйОбъект.СтатусУслуги);
			НовыйОбъект.Удалить("СтатусУслуги");			
		КонецЕсли;
		
		
		ОбъектИлиСсылка = ИИСервер.СоздатьОбъект(НовыйОбъект);
		ЗначениеВРеквизитФормы(ОбъектИлиСсылка, "Объект");
	Иначе
		//Восстановимся из старых данных
		Объект.СтавкаНДС = Справочники.СтавкиНДСБТ.БезНДС;
		Объект.СтавкаНДСКлиента = Справочники.СтавкиНДСБТ.БезНДС;
		
		СуммаНДСКлиента = 0;
		СуммаНДСПоставщикаПоставщика = 0;
		
		ТД = Неопределено; 
		ЭтаФорма.Параметры.Свойство("ТД", ТД);
		Если ЗначениеЗаполнено(ТД) Тогда
			Если ЗначениеЗаполнено(ТД.ПрименяетсяК) Тогда
				//Это Элементы отбора
				Если ТД.РеквизитУстановкиЦены = "Цена" Тогда
					ЦенаПоставщика = ТД.ПрименяетсяК;
					СуммаНДСПоставщика = 0;
					СуммаПоставщика = ТД.ПрименяетсяК;
					Объект.СтавкаНДС = ТД.СтавкаНДС;
					
					ЦенаКлиента = ТД.ПрименяетсяК;
					СуммаНДСКлиента = 0;
					СуммаКлиента = ТД.ПрименяетсяК;
					Объект.СтавкаНДСКлиента = ТД.СтавкаНДС;
					
				ИначеЕсли 	ТД.РеквизитУстановкиЦены = "СуммаНДС18" ИЛИ 
							ТД.РеквизитУстановкиЦены = "СуммаНДС10" ИЛИ
							ТД.РеквизитУстановкиЦены = "СуммаНДС18118" ИЛИ
							ТД.РеквизитУстановкиЦены = "СуммаНДС10110" Тогда
					ЦенаПоставщика = 0;
					СуммаНДСПоставщика = ТД.ПрименяетсяК;
					СуммаПоставщика = 0;
					Объект.СтавкаНДС = ТД.СтавкаНДС;
					
					ЦенаКлиента = 0;
					СуммаНДСКлиента = ТД.ПрименяетсяК;
					СуммаКлиента = 0;
					Объект.СтавкаНДСКлиента = ТД.СтавкаНДС;
				КонецЕсли;
			Иначе
				//Это статические цены
				ЦенаПоставщика = ТД.Цена;
				СуммаНДСПоставщика = ТД.СуммаНДС;
				СуммаПоставщика = ТД.Сумма;
				Объект.СтавкаНДС = ТД.СтавкаНДС;
				
				ЦенаКлиента = ТД.Цена;
				СуммаНДСКлиента = ТД.СуммаНДС;
				СуммаКлиента = ТД.Сумма;
				Объект.СтавкаНДСКлиента = ТД.СтавкаНДС;
			КонецЕсли;
			
			Если ТипЗнч(ТД.СвязаннаяНоменклатура) = Тип("СправочникСсылка.ЭлементСвязаннойНоменклатуры") Тогда 
				//Это старый отбор
				Объект.НоменклатураБТ = ТД.СвязаннаяНоменклатура.НоменклатураБТ;
				ЗаполнитьХарактеристикиНаСервере(Объект.НоменклатураБТ);
			КонецЕсли;
		КонецЕсли;
		
		
		КоличествоКлиента = 1;
	КонецЕсли;
	
	ВПроцентах = Ложь;
	ЭтаФорма.Параметры.Свойство("ВПроцентах", ВПроцентах);
	Если ВПроцентах = Истина Тогда
		ЭтаФорма.Элементы.ЦенаКлиента.Заголовок = "Цена клиента в %"; 
		ЭтаФорма.Элементы.СуммаКлиента.Заголовок = "Сумма клиента в %"; 
		ЭтаФорма.Элементы.СуммаНДСКлиента.Заголовок = "Сумма НДС клиента в %"; 
		
		ЭтаФорма.Элементы.Цена.Заголовок = "Цена в %"; 
		ЭтаФорма.Элементы.Сумма.Заголовок = "Сумма в %"; 
		ЭтаФорма.Элементы.СуммаНДС.Заголовок = "Сумма НДС в %"; 		
	КонецЕсли;
	
	
	Если УправлениеБТВызовСервера.ПолучитьНастройкуСистемы(ПланыВидовХарактеристик.НаборХарактеристикСистемный.КонфигурацияДонор) = "БухгалтерияПредприятия" Тогда
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.Контрагент");
	Иначе	
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Контрагент", "Объект.Контрагент");
    КонецЕсли;
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НоваяСвязь);
	НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
	Элементы.Договор.СвязиПараметровВыбора = НовыеСвязи; 		
	
	Если УправлениеБТВызовСервера.ПолучитьНастройкуСистемы(ПланыВидовХарактеристик.НаборХарактеристикСистемный.КонфигурацияДонор) = "БухгалтерияПредприятия" Тогда
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.Поставщик");
	Иначе	
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Контрагент", "Объект.Поставщик");
    КонецЕсли;
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НоваяСвязь);
	НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
	Элементы.ДоговорСПоставщиком.СвязиПараметровВыбора = НовыеСвязи; 		
	
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("СправочникСсылка.СтавкиНДСБТ"));
	ОписаниеТиповС = Новый ОписаниеТипов(Массив);
	Элементы.СтавкаНДС.ОграничениеТипа = ОписаниеТиповС;
	Элементы.СтавкаНДСКлиента.ОграничениеТипа = ОписаниеТиповС;
	
	
КонецПроцедуры




&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ИзменениеТабличнойЧастиУслуги" И Параметр = Объект Тогда
		ЭтаФорма.ОбновитьОтображениеДанных();
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()
	Объект.Партнер = Объект.Контрагент.Партнер;
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	КонтрагентПриИзмененииНаСервере();
КонецПроцедуры

