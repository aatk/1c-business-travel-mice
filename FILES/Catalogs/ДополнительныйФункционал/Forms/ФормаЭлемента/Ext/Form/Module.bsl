&НаКлиенте
Функция ПолучитьНовуюВерсию(ТекущаяВерсия)

	Версия = "0.0.0.1";
	Если ТекущаяВерсия <> "" Тогда
		МассивВерсий = СтрРазделить(ТекущаяВерсия,".",Истина);
		МассивВерсий[3] = Строка(Число(МассивВерсий[3])+1); 		
		Версия = СтрСоединить(МассивВерсий, ".");
	КонецЕсли;
	
	Возврат Версия;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьПослеВыбораФайла(Пар1, Пар2) Экспорт
	
	Если ТипЗнч(Пар1) = Тип("Массив") Тогда
		
	    Для Каждого ИмяФайла Из Пар1 Цикл
			Файл = Новый Файл(ИмяФайла);
			Имя = Файл.Имя;
						
			ДД = Новый ДвоичныеДанные(ИмяФайла);
			СсылкаНаФайлВоВременномХранилище = ПоместитьВоВременноеХранилище(ДД, ЭтаФорма.УникальныйИдентификатор);
			
			Модифицированность = Истина; 
			Объект.НаименованиеОбработки = Имя;
			Объект.МинимальнаяВерсияСистемы = УправлениеБТВызовСервера.ПолучитьВерисиюБТ();
			Объект.Версия = ПолучитьНовуюВерсию(Объект.Версия); 
		КонецЦикла;
			
	Иначе
	    ПоказатьПредупреждение(,НСтр("ru = 'Файл не выбран!'; en = 'File not selected!'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеВыбораФайлаСохранение(Пар1, Пар2) Экспорт
	
	Если ТипЗнч(Пар1) = Тип("Массив") Тогда
		
		Для Каждого ИмяФайла Из Пар1 Цикл
			СсылкаНаФайлВИБ = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ВнешняяОбработка");			
			ПолучитьФайл(СсылкаНаФайлВИБ, ИмяФайла, Ложь);
		КонецЦикла;
		
	Иначе
	    ПоказатьПредупреждение(,НСтр("ru = 'Файл не выбран!'; en = 'File not selected!'"));
	КонецЕсли;

КонецПроцедуры



&НаКлиенте
Процедура ОткрытьОбработку(Команда)
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	Фильтр = "Поддерживаемые файлы (*.epf;*.erf;*.cf;*.cfe)|*.epf;*.erf;*.cf;*.cfe";
	
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = "Выберите файлы";
	
	Оп = Новый ОписаниеОповещения("ВыполнитьПослеВыбораФайла", ЭтотОбъект, "");
	ДиалогОткрытияФайла.Показать(Оп);
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОбработку(Команда)
	
	Если Объект.НаименованиеОбработки = "" Тогда
		ПоказатьПредупреждение(,"У вас нет сохраненного в базе обработки");
	Иначе
		Режим = РежимДиалогаВыбораФайла.Сохранение;
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
		ДиалогОткрытияФайла.ПолноеИмяФайла = "";
		Фильтр = "Поддерживаемые файлы (*.epf;*.erf;*.cf;*.cfe)|*.epf;*.erf;*.cf;*.cfe";
		
		ДиалогОткрытияФайла.Фильтр = Фильтр;
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = "Выберите файлы";
		ДиалогОткрытияФайла.ПолноеИмяФайла = Объект.НаименованиеОбработки;
		
		Оп = Новый ОписаниеОповещения("ВыполнитьПослеВыбораФайлаСохранение", ЭтотОбъект, "");
		ДиалогОткрытияФайла.Показать(Оп);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(СсылкаНаФайлВоВременномХранилище) Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(СсылкаНаФайлВоВременномХранилище);
		ТекущийОбъект.ВнешняяОбработка = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
	КонецЕсли;       

КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(СсылкаНаФайлВоВременномХранилище) Тогда
		УдалитьИзВременногоХранилища(СсылкаНаФайлВоВременномХранилище);
		СсылкаНаФайлВоВременномХранилище = "";
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ИмяСкриптаШиныПриИзмененииНаСервере()
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	МакетСкрипта = ОбъектСервер.ПолучитьМакет("ДемоСкриптаШины");
	Объект.СкриптШины = МакетСкрипта.ПолучитьТекст();
	Объект.СкриптШины = СтрЗаменить(Объект.СкриптШины, "%ex_component%", Объект.ИмяСкриптаШины);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаСкрипта(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Да Тогда
		ИмяСкриптаШиныПриИзмененииНаСервере();
    КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура ИмяСкриптаШиныПриИзменении(Элемент)
	Режим = РежимДиалогаВопрос.ДаНет;
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаСкрипта", ЭтаФорма, Параметры);
	ПоказатьВопрос(Оповещение, "Изменить скрипт?", Режим, 0, КодВозвратаДиалога.Нет, "Обновление скрипта");
КонецПроцедуры



&НаСервере
Процедура СобратьПакетМаркетплейсаИОтправитьНаСерверНаСервере()
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Результат = МаркетПлейсВызовСервера.СоздатьМодульМаркетплейса(ОбъектСервер.Ссылка);	
	Если Результат.Результат Тогда
		МаркетПлейсВызовСервера.ОтправитьМодульМаркетплейса(Результат.ДД, ОбъектСервер);
	    ЭтаФорма.Прочитать();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СобратьПакетМаркетплейсаИОтправитьНаСервер(Команда)
	Если НЕ ЭтаФорма.Модифицированность Тогда
		СобратьПакетМаркетплейсаИОтправитьНаСерверНаСервере();
	Иначе
		Сообщить("Сохраните элемент");
	КонецЕсли;
КонецПроцедуры



&НаСервере
Функция СоздатьКомпонентИСохранитьНаДискНаСервере()
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Результат = МаркетПлейсВызовСервера.СоздатьМодульМаркетплейса(ОбъектСервер.Ссылка);
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ВыполнитьПослеВыбораКаталогаСохранение(Пар1, Пар2) Экспорт
	
	Если ТипЗнч(Пар1) = Тип("Массив") Тогда
		
		Для Каждого ИмяФайла Из Пар1 Цикл
			СсылкаНаФайлВИБ = СоздатьКомпонентИСохранитьНаДискНаСервере(); //ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ВнешняяОбработка");			
			Если СсылкаНаФайлВИБ.Результат Тогда
				СсылкаНаФайлВоВременномХранилище = ПоместитьВоВременноеХранилище(СсылкаНаФайлВИБ.ДД);
				ПолучитьФайл(СсылкаНаФайлВоВременномХранилище, ИмяФайла+"\"+Объект.Наименование+".zip", Ложь);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
	    ПоказатьПредупреждение(,НСтр("ru = 'Файл не сформирован!'; en = 'File not creator!'"));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьКомпонентИСохранитьНаДиск(Команда)
	
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = "Выберите каталог";
	
	Оп = Новый ОписаниеОповещения("ВыполнитьПослеВыбораКаталогаСохранение", ЭтотОбъект, "");
	ДиалогОткрытияФайла.Показать(Оп);		
	
КонецПроцедуры

&НаСервере
Процедура СброситьОписаниеНаСервере()
	Описание = Справочники.ДополнительныйФункционал.ПолучитьМакет("ПримерОписанияКомпоненты").ПолучитьТекст();
	Объект.ШаблонОписания = Описание;
КонецПроцедуры

&НаКлиенте
Процедура СброситьОписание(Команда)
	Режим = РежимДиалогаВопрос.ДаНет;
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Параметры);
	ПоказатьВопрос(Оповещение, "Вы уверены, что хотите сбросить описание компоненты?", Режим, 0);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
	СброситьОписаниеНаСервере();
КонецПроцедуры 


&НаКлиенте
Процедура РасшаритьКомпоненту(Команда)
	МаркетПлейсКлиент.РасшаритьКомпонент(Объект.Ссылка);
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьСобытияНаСервере()
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	ОбъектСервер.ЗаполнитьСписокОбрабатываемыхСобытий();
	ЗначениеВРеквизитФормы(ОбъектСервер, "Объект");
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьСобытия(Команда)
	ЗаполнитьСобытияНаСервере();
КонецПроцедуры


&НаСервере
Процедура ОбновитьОписаниеПоШаблонуНаСервере()
	Описание = Объект.ШаблонОписания;
	Для Каждого Реквизит ИЗ Метаданные.Справочники.ДополнительныйФункционал.Реквизиты Цикл
		Попытка
			Описание = СтрЗаменить( Описание, "%"+Реквизит.Имя+"%", Объект[Реквизит.Имя]);
		Исключение
		КонецПопытки;
	КонецЦикла;
	Описание = СтрЗаменить( Описание, "%Наименование компоненты%", Объект.Наименование);
	Описание = СтрЗаменить( Описание, "%ПубличнаяКомпонента%", Формат(Объект.Наименование,"БЛ=Приватная; БИ=Публичная"));
	Объект.Описание = Описание;
КонецПроцедуры


&НаКлиенте
Процедура ОбновитьОписаниеПоШаблону(Команда)
	ОбновитьОписаниеПоШаблонуНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ОтправитьНастройкиНаСерверШины(Команда)
	МаркетПлейсВызовСервера.ОтправитьНастройкиНаСерверШины(Объект.Ссылка);
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьИзображенияВШинуНаСервере()
	// Вставить содержимое обработчика.
КонецПроцедуры


&НаКлиенте
Процедура ЗагрузитьИзображенияВШину(Команда)
	ЗагрузитьИзображенияВШинуНаСервере();
КонецПроцедуры

