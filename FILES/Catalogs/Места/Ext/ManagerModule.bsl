
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ДанныеВыбора = Новый СписокЗначений();
	СтандартнаяОбработка = Ложь;
	
	СтрокаПоиска = ?(Параметры.СтрокаПоиска = Неопределено, "", Параметры.СтрокаПоиска);
	
	СловоИНГ = УправлениеБТВызовСервера.СменитьРаскладкуRUEN(СтрокаПоиска);
	СловоРУС = УправлениеБТВызовСервера.СменитьРаскладкуENRU(СтрокаПоиска);
	
	Запрос = Новый Запрос;	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 51
	               |	Места.Наименование,
	               |	Места.ВидМеста,
	               |	Места.Код,
	               |	Места.ПометкаУдаления,
	               |	Места.Ссылка
	               |ИЗ
	               |	Справочник.Места КАК Места
	               |ГДЕ
	               |	Места.Наименование ПОДОБНО &СтрокаВводаРус
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	Места.Наименование,
	               |	Места.ВидМеста,
	               |	Места.Код,
	               |	Места.ПометкаУдаления,
	               |	Места.Ссылка
	               |ИЗ
	               |	Справочник.Места КАК Места
	               |ГДЕ
	               |	Места.Наименование ПОДОБНО &СтрокаВводаАнг";	
	
	Запрос.УстановитьПараметр("СтрокаВводаАнг", СловоИНГ + "%" );	
	Запрос.УстановитьПараметр("СтрокаВводаРус", СловоРУС + "%" );	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ДанныеВыбора.НайтиПоЗначению(Выборка.Ссылка) = Неопределено Тогда
			
			Представление = Выборка.Наименование + " (" +Выборка.ВидМеста + ") "+ Выборка.Код;
			
			Если Выборка.ПометкаУдаления Тогда
				СтруктураЗначение = Новый Структура("Значение,ПометкаУдаления", Выборка.Ссылка, Выборка.ПометкаУдаления);
				ДанныеВыбора.Добавить(СтруктураЗначение, Представление,,БиблиотекаКартинок.ПомеченныйНаУдалениеЭлемент);
			Иначе
				ДанныеВыбора.Добавить(Выборка.Ссылка, Представление);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
