
&НаКлиенте
Процедура СписокОбъектовПриАктивизацииСтроки(Элемент)
	
	Если Элементы.СписокОбъектов.ТекущиеДанные <> Неопределено Тогда
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ИдентификаторНаСтрокуОбъекта", Элементы.СписокОбъектов.ТекущиеДанные.ИдентификаторСтроки);
		ОтборКоманд = Новый ФиксированнаяСтруктура(СтруктураОтбора);
		Элементы.СписокКоманд.ОтборСтрок =  ОтборКоманд;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура СписокОбъектовПутьКОбъектуМетаданныхПриИзменении(Элемент)
	ТД = Элементы.СписокОбъектов.ТекущиеДанные;
	Если ТД <> Неопределено Тогда
		ТД.ПредставлениеОбъекта = Строка(ТД.ОбъектМетаданных);
		Если НЕ ЗначениеЗаполнено(ТД.ИдентификаторСтроки) Тогда
			ТД.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор());
			ЗаполнитьКоманды(ТД.ИдентификаторСтроки, ТД.ОбъектМетаданных);
			СписокОбъектовПриАктивизацииСтроки(Элемент);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если НоваяСтрока Тогда
		ТД = Элементы.СписокОбъектов.ТекущиеДанные;
		Если НЕ ЗначениеЗаполнено(ТД.ИдентификаторСтроки) Тогда
			ТД.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор());
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКоманды(ИдентификаторСтроки, ОбъектМетаданных)
		
	МассивСтрок = Объект.СписокКоманд.НайтиСтроки(Новый Структура("ИдентификаторНаСтрокуОбъекта", ИдентификаторСтроки));
	Для Каждого СтараяСтрока ИЗ МассивСтрок Цикл
		Объект.СписокКоманд.Удалить(СтараяСтрока);
	КонецЦикла;
	
	ВсеКоманды = Справочники.ДополнительныеОтчетыИПечатныеФормы.ПолучитьВсеКомандыПоОбъекту(ОбъектМетаданных.ПолноеИмя);
	Для Каждого НоваяКоманда ИЗ ВсеКоманды Цикл		
		Команда = Объект.СписокКоманд.Добавить();
		Команда.ИдентификаторНаСтрокуОбъекта = ИдентификаторСтроки;
		Команда.ИмяКоманды = НоваяКоманда.ИмяКоманды;
		Команда.ИмяКомандыURL = УправлениеБТВызовСервера.ТранслитерацияRUEN(НоваяКоманда.ИмяКоманды);
		Команда.ПредставлениеКоманды = НоваяКоманда.ПредставлениеКоманды;
		Команда.Использовать = НоваяКоманда.Использовать; 
		Команда.СсылкаНаПечатнуюФорму = НоваяКоманда.Ссылка;
		Команда.ФорматВывода = "pdf";
	КонецЦикла;

КонецПроцедуры


&НаКлиенте
Процедура УдалитьСвязанныеСтроки(ИдентификаторСтроки)
	
	ДляУдаления = Новый Массив;
	Для Каждого СтрокаКоманд ИЗ Объект.СписокКоманд Цикл
		Если СтрокаКоманд.ИдентификаторНаСтрокуОбъекта = ИдентификаторСтроки Тогда
			ДляУдаления.Добавить(СтрокаКоманд);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаУдаления ИЗ ДляУдаления Цикл
		Объект.СписокКоманд.Удалить(СтрокаУдаления);
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовПередУдалением(Элемент, Отказ)
	
	ТД = Элементы.СписокОбъектов.ТекущиеДанные;
	Если ТД <> Неопределено Тогда
		УдалитьСвязанныеСтроки(ТД.ИдентификаторСтроки);
	КонецЕсли;

КонецПроцедуры
