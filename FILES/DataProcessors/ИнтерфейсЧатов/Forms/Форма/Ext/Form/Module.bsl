
&НаСервере
Процедура ЗагрузитьЧатНаСервере()
	
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	
	ШаблонСообщения = ОбъектСервер.ПолучитьМакет("СообщениеПользователя").ПолучитьТекст();
	ШаблонСообщенияОБТ = ОбъектСервер.ПолучитьМакет("СообщениеПользователяОБТ").ПолучитьТекст();
	//"
	//|<DIV class=""[Расположение]"">
	//|    <div class=""picture"">[Лого]</div>
	//|    <div class=""message"">[Сообщение]</div>
	//|</DIV>
	//|";
	
	Запрос = Новый Запрос;
	//Запрос.УстановитьПараметр("ПользовательОБТ", Объект.ПользовательОБТ);
	Запрос.УстановитьПараметр("Объект", Объект.ОбъектИнтерфейса);
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 100
	               |	ЧатСПользователямиОБТ.Период КАК Период,
	               |	ЧатСПользователямиОБТ.ПользовательОБТ КАК ПользовательОБТ,
	               |	ЧатСПользователямиОБТ.Пользователь КАК Пользователь,
	               |	ЧатСПользователямиОБТ.Объект КАК Объект,
	               |	ЧатСПользователямиОБТ.Система КАК Система,
	               |	ЧатСПользователямиОБТ.Владелец КАК Владелец,
	               |	ЧатСПользователямиОБТ.Сообщение КАК Сообщение,
	               |	ЧатСПользователямиОБТ.ПрикрепленныеДанные КАК ПрикрепленныеДанные
	               |ИЗ
	               |	РегистрСведений.ЧатСПользователямиОБТ КАК ЧатСПользователямиОБТ
	               |ГДЕ
	               |	ЧатСПользователямиОБТ.Объект = &Объект
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ЧатСПользователямиОБТ.Период УБЫВ";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Чат = "";
	Пока Выборка.Следующий() Цикл
		
		ПараметрыШаблона = Новый Структура("Расположение, Лого, Сообщение, ДатаВремя", 
			?(Выборка.Владелец = Перечисления.ВладелецСообщенияЧата.Клиент, "left", "rigth"),
			?(Выборка.Владелец = Перечисления.ВладелецСообщенияЧата.Клиент, Выборка.ПользовательОБТ, Выборка.Пользователь),
			Выборка.Сообщение,
			Выборка.Период
		);	
		
		Если Выборка.Владелец = Перечисления.ВладелецСообщенияЧата.Клиент Тогда
			СообщениеЧата = ШаблонСообщенияОБТ;
		Иначе
			СообщениеЧата = ШаблонСообщения;		
		КонецЕсли;
		
		Для Каждого ПараметрШаблона ИЗ ПараметрыШаблона Цикл
			СообщениеЧата = СтрЗаменить(СообщениеЧата, "["+ПараметрШаблона.Ключ+"]", ПараметрШаблона.Значение);	
		КонецЦикла;
		
		Чат = СообщениеЧата + Чат;
	КонецЦикла;
	
	Макет = ОбъектСервер.ПолучитьМакет("МакетЧата").ПолучитьТекст();
	Макет = СтрЗаменить(Макет, "[Чат]", Чат);
	
	HTMLДок = Макет;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЧат(Команда)
	ЗагрузитьЧатНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОтправитьНаСервере()
	
	Вопрос = Новый Структура("Владелец, ПользовательОБТ, Пользователь, Объект, Система, Сообщение",
		Перечисления.ВладелецСообщенияЧата.Сотрудник, 
		Объект.ПользовательОБТ, 
		ПараметрыСеанса.ТекущийПользователь, 
		Объект.ОбъектИнтерфейса, 
		Перечисления.СистемыДляЧатов.БТ, 
		Сообщение);

	МоментВремени = ТекущаяДата();
	Чат = РегистрыСведений.ЧатСПользователямиОБТ.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Чат, Вопрос);
	Чат.Период = МоментВремени;
	Чат.КлючСообщения = Новый УникальныйИдентификатор();
	Чат.Записать(Истина);
	
	
	Сообщение = "";

КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	ОтправитьНаСервере();
	ЗагрузитьЧатНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ОбъектИнтерфейсаПриИзменении(Элемент)
	ЗагрузитьЧатНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ОбъектыОбщенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ВыбраннаяСтрока <> Неопределено Тогда
		Объект.ОбъектИнтерфейса = Элемент.ТекущиеДанные.ОбъектОбщения;
		ОбъектИнтерфейсаПриИзменении(Элемент);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьЧатыНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПользовательОБТ", Объект.ПользовательОБТ);
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(ЧатСПользователямиОБТ.Период) КАК Период,
	               |	ЧатСПользователямиОБТ.ПользовательОБТ КАК ПользовательОБТ
	               |ПОМЕСТИТЬ ПоследниеСообщенияПоОбъектам
	               |ИЗ
	               |	РегистрСведений.ЧатСПользователямиОБТ КАК ЧатСПользователямиОБТ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЧатСПользователямиОБТ.ПользовательОБТ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 20
	               |	МАКСИМУМ(ПоследниеСообщенияПоОбъектам.Период) КАК Период,
	               |	ПоследниеСообщенияПоОбъектам.ПользовательОБТ КАК ПользовательОБТ
	               |ИЗ
	               |	ПоследниеСообщенияПоОбъектам КАК ПоследниеСообщенияПоОбъектам
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПоследниеСообщенияПоОбъектам.ПользовательОБТ
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период УБЫВ";
	Выгрузка = Запрос.Выполнить().Выгрузить();
	ПользователиОБТ.Загрузить(Выгрузка);
	
КонецПроцедуры

&НаСервере
Процедура ПользовательОБТПриИзмененииНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПользовательОБТ", Объект.ПользовательОБТ);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЧатСПользователямиОБТ.Объект КАК Объект,
	               |	МАКСИМУМ(ЧатСПользователямиОБТ.Период) КАК Период
	               |ПОМЕСТИТЬ ПоследниеСообщенияПоОбъектам
	               |ИЗ
	               |	РегистрСведений.ЧатСПользователямиОБТ КАК ЧатСПользователямиОБТ
	               |ГДЕ
	               |	ЧатСПользователямиОБТ.ПользовательОБТ = &ПользовательОБТ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЧатСПользователямиОБТ.Объект
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 20
	               |	ПоследниеСообщенияПоОбъектам.Объект КАК ОбъектОбщения,
	               |	МАКСИМУМ(ПоследниеСообщенияПоОбъектам.Период) КАК Период
	               |ИЗ
	               |	ПоследниеСообщенияПоОбъектам КАК ПоследниеСообщенияПоОбъектам
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПоследниеСообщенияПоОбъектам.Объект
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период УБЫВ";
	Выгрузка = Запрос.Выполнить().Выгрузить();
	ОбъектыОбщения.Загрузить(Выгрузка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательОБТПриИзменении(Элемент)
	ПользовательОБТПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗагрузитьЧатыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	//Обновим списки чатов при любом событии
	ЗагрузитьЧатыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПользователиОБТВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если ЭтаФорма.Элементы.ПользователиОБТ.ТекущиеДанные <> Неопределено Тогда
		Объект.ПользовательОБТ = ЭтаФорма.Элементы.ПользователиОБТ.ТекущиеДанные.ПользовательОБТ;
		ПользовательОБТПриИзмененииНаСервере();
		Если ОбъектыОбщения.Количество() > 0 Тогда
			Объект.ОбъектИнтерфейса = ОбъектыОбщения[0].ОбъектОбщения;
			ОбъектИнтерфейсаПриИзменении(Элемент);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
